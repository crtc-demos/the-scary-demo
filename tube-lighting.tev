# depth ramp lookup, shadow 1
stage 0:
  cr1.rgb = texmap1[texcoord1].arr;

# shadow buffer sample 1
stage 1:
  tev.rgb = texmap2[texcoord2].arr;

# sample 1 comparison
stage 2:
  tev.rgb = (cr1{gr} > tev{gr}) ? 1 : 0;

# shadow buffer sample 2
stage 3:
  cr0.rgb = texmap2[texcoord3].arr;

# sample 2 comparison
stage 4:
  cr0.rgb = (cr1{gr} > cr0{gr}) ? 1 : 0;

stage 5:
  tev.rgb = clamp (tev.rgb * cr0.rgb);

# shadow buffer sample 3
stage 6:
  cr0.rgb = texmap2[texcoord4].arr;

# sample 3 comparison
stage 7:
  cr0.rgb = (cr1{gr} > cr0{gr}) ? 1 : 0;

# shadow (1) or lit (0)
stage 8:
  tev.rgb = clamp (tev.rgb * cr0.rgb);

# base colour
stage 9:
  cr0.rgb = texmap0[texcoord0].rgb;

# lit colour
stage 10:
  cr1.rgb = clamp (chan0.aaa + cr0.rgb * chan0.rgb);

# shadow colour
stage 11:
  cr0.rgb = cr0.rgb * k0.rgb;

# "diffuse" channel 1 light colour isn't affected by shadow... it might be a
# big light source? Anyway we don't have enough stages to do the 3-sample +
# 16-bit comparison trick for two different shadows.
stage 12:
  tev.rgb = clamp (chan1.rgb + mix (cr1.rgb, cr0.rgb, tev.rgb));
